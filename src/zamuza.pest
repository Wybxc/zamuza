Program = { SOI ~ (Rule | NetEquation | Interface)+ ~ EOI }

Rule               = { RuleTermPair ~ "=>" ~ RuleEquations }
RuleTermPair      = { RuleTermLeftRight | RuleTermRightLeft }
RuleTermLeftRight = { RuleTerm ~ ">>" ~ RuleTerm }
RuleTermRightLeft = { RuleTerm ~ "<<" ~ RuleTerm }
RuleTerm           = { Agent ~ ("(" ~ Name ~ (Comma ~ Name)* ~ ")")? }
RuleEquations      = { "_" | Equation ~ (Comma ~ Equation)* }

NetEquation       = _{ "::" ~ Equation }
Equation          =  { EquationLeftRight | EquationRightLeft }
EquationLeftRight =  { Term ~ "->" ~ Term }
EquationRightLeft =  { Term ~ "<-" ~ Term }

Interface = { "::" ~ ("$" ~ "<-" ~ Term | Term ~ "->" ~ "$") }

Term    = { Name | Agent ~ ("(" ~ Term ~ (Comma ~ Term)* ~ ")")? }
Name    = { NameIn | NameOut }
NameIn  = { "#" ~ Ident }
NameOut = { "@" ~ Ident }
Agent   = { Ident }

Ident     = @{ Letter ~ LetterNum* ~ "'"* }
LetterNum =  { Letter | '0'..'9' }
Letter    =  { LETTER | "_" }

Comma = _{ "," }

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT    = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
