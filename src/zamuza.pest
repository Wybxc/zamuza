Program = { SOI ~ (Rule | Net)+ ~ EOI }

Rule              = { RuleTermPair ~ "=>" ~ RuleEquations }
RuleTermPair      = { RuleTermLeftRight | RuleTermRightLeft }
RuleTermLeftRight = { RuleTerm ~ ">>" ~ RuleTerm }
RuleTermRightLeft = { RuleTerm ~ "<<" ~ RuleTerm }
RuleTerm          = { Agent ~ ("(" ~ Name ~ (Comma ~ Name)* ~ Comma? ~ ")")? }
RuleEquations     = { "_" | Equation ~ (Comma ~ Equation)* ~ Comma? }

Net          = { Ident ~ "<|" ~ Interfaces ~ "|>" ~ NetEquations }
NetEquations = { "_" | Equation ~ (Comma ~ Equation)* ~ Comma? }

Equation          = { EquationLeftRight | EquationRightLeft }
EquationLeftRight = { Term ~ "->" ~ Term }
EquationRightLeft = { Term ~ "<-" ~ Term }

Interfaces = { Term ~ (Comma ~ Term)* ~ Comma? }

Term    = { Name | Agent ~ ("(" ~ Term ~ (Comma ~ Term)* ~ ")")? }
Name    = { NameIn | NameOut }
NameIn  = { "#" ~ Ident }
NameOut = { "@" ~ Ident }
Agent   = { Ident }

Ident     = @{ Letter ~ LetterNum* ~ "'"* }
LetterNum =  { Letter | '0'..'9' }
Letter    =  { LETTER | "_" }

Comma = _{ "," | ";" }

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT    = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
