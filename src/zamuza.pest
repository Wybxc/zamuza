Program = { SOI ~ (Rule | NetEquation | Interface)+ ~ EOI }

Rule               = { (RuleTermsLeftRight | RuleTermsRightLeft) ~ "=>" ~ RuleEquations }
RuleTermsLeftRight = { RuleTerm ~ ">>" ~ RuleTerm }
RuleTermsRightLeft = { RuleTerm ~ "<<" ~ RuleTerm }
RuleTerm           = { Agent ~ ("(" ~ Name ~ (Comma ~ Name)* ~ ")")? }
RuleEquations      = { "_" | Equation ~ (Comma ~ Equation)* }

NetEquation       = _{ "::" ~ Equation }
Equation          =  { EquationLeftRight | EquationRightLeft }
EquationLeftRight =  { Term ~ "->" ~ Term }
EquationRightLeft =  { Term ~ "<-" ~ Term }

Interface = { "$" ~ "=" ~ Term }

Term    = { Name | Agent ~ ("(" ~ Term ~ (Comma ~ Term)* ~ ")")? }
Name    = { NameIn | NameOut }
NameIn  = { "#" ~ Ident }
NameOut = { "@" ~ Ident }
Agent   = { Ident }

Ident     = @{ Letter ~ LetterNum* ~ "'"* }
LetterNum =  { Letter | '0'..'9' }
Letter    =  { LETTER | "_" }

Comma = _{ "," }

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT    = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
